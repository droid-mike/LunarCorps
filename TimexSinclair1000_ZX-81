100 REM BASIC MONTH 4: LUNAR CORPS
110 REM HTTP://REDDIT.COM/R/RETROBATTLESTATIONS
120 REM ORIGINALLY WRITTEN BY FOZZTEXX
130 REM PORTED TO ZX81/TS1000 BY U/DROID_MIKE
140 REM CLEAR SCREEN AND SET VARIABLES
142 CLS
143 GOSUB 6000
144 PRINT
146 PRINT "PLEASE WAIT 60 SECONDS TO START"
149 RAND
150 DIM Z$(2,5)
155 DIM A$(12,30)
156 LET B$=" "
157 LET I$=""
160 LET N$=""
161 LET J$="                                "
162 DIM G(12)
163 DIM F(12)
164 REM LOAD UP MISSION NAMES
165 GOSUB 5000
166 REM SET ZX VALUE TO 205 FOR UK ZX81, 195 FOR TS1000
167 LET ZX=205
170 LET MI=1
172 LET RA=0
175 LET SC=0
200 LET PEAK=0
205 LET Q$=" "
206 LET TH=0
220 LET P$ = " " + CHR$(6) + CHR$(134) + CHR$(9)
230 LET COLS = 31
235 LET ROWS = 21
237 PRINT AT ROWS-1,0;""
295 REM DRAW LUNAR SURFACE--USE STORED SCREEN IF AVAILABLE
297 IF I$="" THEN GOSUB 2500
300 IF i$<>"" THEN PRINT AT 19,0;I$
302 LET B$=" "
305 REM DEMO MODE
307 GOSUB 6000
309 PRINT AT 1,5;"PRESS A KEY TO PLAY"
310 LET YS = SQR(2) * SQR(ROWS - TL + ZL) - 1
320 LET XE = INT((ZE - ZS) / 2) + ZS
330 LET XV = XE / YS
335 LET YV = 0
340 LET X = 0
345 LET Y = 1
347 LET XX=X
348 LET YY=Y
420 FOR I = 1 TO YS
440 LET YV = YV + 1
450 LET X = X + XV - 1
455 LET Y = Y + YV
460 LET TH=0
465 IF I >= YS-2 THEN LET TH=100
467 IF INKEY$<>"" THEN GOTO 3000
470 GOSUB 1500
480 IF INKEY$<>"" THEN GOTO 3000
490 NEXT I
505 GOSUB 1500
507 IF INKEY$<>"" THEN GOTO 3000
510 LET X = (ZE - ZS) / 2 + ZS - 3
515 LET Y = ROWS - TL + ZL 
517 LET TH=0
520 IF INKEY$<>"" THEN GOTO 3000
525 GOSUB 1500
530 REM LANDING SUBROUTINE
540 GOSUB 7500
570 FOR I=1 TO 100
580 IF INKEY$<>"" THEN GOTO 3000
590 NEXT I
595 PRINT AT YY,XX;"                              "
600 PRINT AT Y,X-1;"     "
605 PRINT AT Y+1,X-1;"     "
610 PRINT AT Y+2,X-1;CHR$(10);CHR$(10);CHR$(10);CHR$(10);CHR$(10)
620 GOTO 310
1500 REM DRAW SPRITE IN S$ AT X,Y
1501 LET S$ = "(" + CHR$(138) + ")"
1502 IF B$="M" THEN LET S$(1)=CHR$(9)
1503 IF B$="N" THEN LET S$(3)=CHR$(9)
1505 LET T$ = CHR$(6)+ " " + CHR$(134)
1506 IF TH>0 THEN LET T$(2) = CHR$(10)
1508 IF TH>50 THEN LET T$(2) = CHR$(136)
1510 IF Y < 1 OR Y > ROWS OR X < 1 OR X > COLS THEN RETURN 
1520 REM SET THE LEFT MARGIN TO THE X POSITION
1522 PRINT AT YY,XX;"   "
1524 PRINT AT YY+1,XX;"   "
1530 PRINT AT Y,X;S$
1540 PRINT AT Y+1,X;T$
1550 LET XX=X
1560 LET YY=Y
1570 RETURN
2500 REM DRAW LUNAR SURFACE
2505 LET RC=0
2507 FAST
2510 LET DIDUP = 0
2512 LET L$ = ""
2514 FOR I = 1 TO COLS + 4
2516 LET L$ = L$ + " "
2518 NEXT I
2520 LET ZS = 0
2522 LET ZE = 0
2524 LET TL = 0
2530 LET M$ = "  "
2532 LET UP = DIDUP
2534 IF NOT UP AND L$(3) <> " " THEN LET UP = 1
2540 IF NOT DIDUP AND UP THEN LET DIDUP = UP
2550 LET Z1 = 0
2552 LET Z2 = 0
2560 FOR I = 3 TO COLS + 2
2570 LET C$ = M$(I-2)
2575 LET D$ = M$(I-1)
2580 LET E$ = L$(I-1)
2585 LET F$ = L$(I)
2590 LET G$ = L$(I+1)
2592 IF LEN(L$)<I+2 THEN LET L$=L$+" "
2595 LET H$ = L$(I+2)
2600 IF NOT(PEAK = 0 AND L$(I-1 TO I+2) = "    ")THEN GOTO 2610
2602 LET PEAK = INT(RND * 3)
2604 LET PEAK = PEAK + (PEAK = 2)
2606 LET PEAK = PEAK * INT(RND * 2)
2610 IF  D$ = CHR$(6) AND F$ = " " THEN LET PEAK = 2
2620 IF (C$ = CHR$(9) OR C$ = CHR$(134)) AND D$ = " " THEN LET PEAK = 0
2630 IF F$ <> " " THEN LET PEAK = 0
2640 IF (E$ = CHR$(134) OR E$ = CHR$(9)) AND F$ = " " THEN LET PEAK = 2
2650 IF D$ = CHR$(9) AND H$ = CHR$(6) THEN LET PEAK = 3
2660 IF (D$ = CHR$(134) OR D$ = " " OR D$ = CHR$(9)) AND F$ = " " AND (G$ = CHR$(9) OR G$ = CHR$(6)) THEN LET PEAK = 1
2670 IF D$ = CHR$(134) AND H$ <> " " THEN LET PEAK = 3
2680 IF (G$ = CHR$(6) OR G$ = CHR$(9)) THEN LET PEAK = 1
2690 IF F$ <> " " THEN LET PEAK = 0
2700 IF UP AND PEAK <> 2 THEN LET PEAK = 0
2710 IF PEAK = 1 THEN LET UP = 1
2720 IF PEAK = 2 THEN LET UP = 0
2730 LET Q$ = P$(PEAK + 1)
2732 PRINT Q$;
2734 LET M$ = M$ + Q$
2740 IF PEAK = 3 AND NOT Z1 THEN LET Z1 = I
2750 IF PEAK <> 3 AND D$ = CHR$(9) THEN LET Z2 = I
2760 IF NOT(Z1 AND Z2) THEN GOTO 2770
2761 LET Z3 = Z1
2762 LET Z4 = Z2
2763 LET Z1 = 0
2764 LET Z2 = 0
2765 IF NOT(Z4 - Z3 >= 3 AND Z3 > ZS) THEN GOTO 2770
2766 LET ZS = Z3
2767 LET ZE = Z4
2768 LET ZL = TL
2770 IF PEAK <> 3 OR (C$ = CHR$(9) AND D$ = CHR$(9)) THEN LET PEAK = 0
2780 NEXT I
2790 LET L$ = M$
2800 LET C2 = 0
2810 FOR I = 3 TO COLS+2
2820 IF L$(I) = " " THEN LET C2 = C2 + 1
2830 NEXT I
2840 LET TL = TL + 1
2845 LET RC = RC + 1
2850 IF C2 >= COLS OR RC > 2 THEN GOTO 2860
2852 REM PRINT
2854 SCROLL
2856 GOTO 2530
2860 LET ZS = ZS - 2
2862 LET ZE = ZE - 2
2870 REM SAVE THE BOTTOM SCREEN IN I$ FOR QUICK RECALL
2875 LET I$=""
2880 FOR I=0 TO 2
2890 FOR J=0 TO 30
2900 PRINT AT 19+I,J;
2910 LET TMP=PEEK 16398 +256*PEEK 16399
2920 LET I$=I$+CHR$(PEEK(TMP))
2930 NEXT J
2935 LET I$=I$+" "
2940 NEXT I
2950 SLOW
2960 RETURN
3000 REM MAIN GAME
3005 CLS
3010 GOSUB 6000
3015 IF N$<>"" THEN GOTO 3060
3020 PRINT
3030 PRINT "ENTER YOUR NAME, PLEASE"
3040 INPUT N$
3050 PRINT "WELCOME, ENSIGN ";N$
3055 PRINT
3060 PRINT AT 21,0;"-------------------------------"
3070 SCROLL
3080 PRINT "      MISSION BRIEFING"
3085 SCROLL
3090 PRINT  "-------------------------------"
3100 SCROLL
3105 SCROLL
3110 PRINT R$((RA*12)+1 TO (RA*12)+12);" ";N$
3120 SCROLL
3130 SCROLL
3140 PRINT "YOUR NEXT ASSIGNMENT IS TO"
3150 SCROLL
3160 PRINT "COMMAND THE FOLLOWING:"
3170 SCROLL
3180 SCROLL
3190 PRINT A$(MI)(1 TO 15); " MISSION"
3200 SCROLL
3210 SCROLL
3220 PRINT "THE GRAVITY WILL BE ";G(MI);" G"
3230 SCROLL
3235 SCROLL
3240 PRINT AT 20,0;"YOU WILL HAVE ";F(MI);" FUEL UNITS"
3250 SCROLL
3260 SCROLL
3261 PRINT "USE THE ";CHR$(179);" ";CHR$(178);" KEYS FOR SIDE THRUST"
3262 SCROLL
3263 PRINT CHR$(192);CHR$(166);CHR$(192);" KEY ADJUSTS MAIN THRUST"
3264 SCROLL
3265 PRINT "( >0 = MAIN THRUST IS ALWAYS ON)"
3266 SCROLL
3267 PRINT CHR$(192);CHR$(168);CHR$(192);" TOGGLES ON GUIDANCE COMPUTER"
3268 SCROLL
3269 SCROLL
3270 PRINT "CONTROLS ARE SLUGGISH: HOLD KEYS"
3272 SCROLL
3274 SCROLL
3275 IF B$ = "" THEN GOTO 3278
3276 IF B$(1)="Y" THEN GOTO 3292
3278 PRINT "TYPE Y TO ACCEPT OR N TO RETIRE"
3280 INPUT B$
3285 IF B$(1)="N" THEN GOTO 9000
3291 SCROLL
3292 SCROLL
3293 PRINT "WE ARE NOW AT FAR SIDE OF MOON"
3294 SCROLL
3295 IF MI > 1 THEN PRINT "PREPARE FOR 60 SECOND BLACKOUT"
3296 SCROLL
3298 PRINT "LOSS OF COMM SIGNAL IN 3, 2, 1.."
3300 REM SET UP MISSION
3305 REM SECRET MISSION SELECT
3306 IF CODE(B$(1))>27 and CODE(B$(1))<37 THEN LET MI=VAL(B$)
3310 LET XV=3-RND*6
3320 LET YV=RND*(4*G(MI))
3330 LET X=8+RND*16
3340 LET Y=1
3345 LET FU=F(MI)
3346 LET GR=G(MI)
3347 LET TH=0
3348 LET XX=X
3349 LET YY=Y
3350 CLS
3355 PRINT AT ROWS-1,0;""
3358 REM SECRET PRACTICE MODE
3360 IF B$="P" THEN GOTO 3370
3361 REM IF REPEAT LEVEL OR FIRST MISSION, THEN USE MEMORY FOR QUICK DRAW
3362 IF B$<>"R" AND  MI<>1 THEN GOTO 3365
3363 PRINT AT 19,0;I$
3364 GOTO 3372
3365 GOSUB 2500
3366 GOTO 3372
3370 PRINT AT 19,0;CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);CHR$(9);
3372 LET B$=""
3374 REM TUTORIAL
3375 IF MI=1 THEN GOSUB 8000
3380 PRINT AT 0,0;"INITIAL MAIN THRUST LEVEL?(0-99)"
3390 INPUT TH
3400 IF TH < 0 OR TH > 99 THEN GOTO 3390
3405 PRINT AT 6,0;J$
3407 PRINT AT 0,0;J$
3408 PRINT AT 0,0;" ";A$(MI)
3500 REM GAME LOOP                                                              "
3510 IF INKEY$ <> "" THEN LET B$=INKEY$
3520 GOSUB 1500
3522 IF INKEY$ <> "" THEN LET B$=INKEY$
3540 IF FU >= 0 THEN GOTO 3560
3550 LET FU=0
3555 LET TH=0
3560 LET YV=YV+GR
3565 LET YV=YV-((2*GR)*(TH/100))
3570 LET X=X+XV
3580 LET Y=Y+YV
3581 LET FU=FU-(TH/10)
3582 IF FU<20 THEN PRINT AT 0,0;"        ALERT: FUEL LOW         "
3583 IF RND<0.2 THEN PRINT AT 0,0;CHR$(192);"1202 ALARM";CHR$(192);" FLIGHT:";CHR$(192);"GO ON ALARM"
3584 IF INKEY$ <> "" THEN LET B$=INKEY$
3585 GOSUB 4500
3590 IF X>29 THEN LET X=0
3595 IF X<0 THEN LET X=29
3596 IF Y>=0 THEN GOTO 3600
3597 LET Y=0
3598 PRINT AT 0,0;"LANDER TOO HIGH: THRUST SHUTDOWN"
3599 LET TH=0
3600 IF Y>20 THEN GOTO 9500
3700 REM CHECK LANDING CODE HERE
3800 IF Y<17 THEN GOTO 3500
3805 PRINT AT 0,0;"LIGHTS ON, PICKING UP SOME DUST "
3810 PRINT AT Y+1,X;
3820 LET TMP=PEEK 16398 +256*PEEK 16399
3825 REM SUCCESSFUL LANDING?
3830 IF PEEK(TMP)=9 AND PEEK(TMP+2)=9 THEN GOTO 7000
3835 REM NO HIT?
3840 IF PEEK(TMP)=0 AND PEEK(TMP+2)=0 THEN GOTO 3500
3850 REM ELSE CRASH
3860 GOTO 9500
4500 REM CHECK INKEY$, WHICH IS B$
4505 IF B$ = "" THEN RETURN
4510 IF B$ = "C" THEN GOSUB 8500   
4530 IF B$ = "M" THEN LET XV=XV+0.75
4540 IF B$ = "N" THEN LET XV=XV-0.75
4550 IF B$ <> "A" THEN GOTO 4560
4551 IF FU <= 0 THEN GOTO 4558
4552 PRINT AT 0,0;J$
4553 PRINT AT 0,0;"CURRENT THRUST: ";TH;" ENTER 0-99"
4554 INPUT TH
4555 IF TH>99 OR TH <0 THEN GOTO 4554
4556 PRINT AT 0,0;J$
4557 GOTO 4560
4558 PRINT AT 0,0;" THRUST = 0   NO FUEL AVAILABLE "
4560 GOSUB 1500
4565 LET B$=""
4570 RETURN
5000 REM MISSION NAME LOAD
5010 LET A$(1)="APOLLO 11 MOON EAGLE"
5020 LET A$(2)="APOLLO 12 MOON INTREPID"
5030 LET A$(3)="APOLLO 14 MOON ANTARES"
5040 LET A$(4)="APOLLO 15 MOON FALCON"
5050 LET A$(5)="APOLLO 16 MOON ORION"
5060 LET A$(6)="APOLLO 17 MOON CHALLENGER" 
5070 LET A$(7)="VIKING MARS    MARS VIKING"
5075 LET A$(8)="MAGELLAN VENUS VENUS MAGELLAN"
5080 LET A$(9)="MARS ROVER 1   MARS SOJOURNER"
5090 LET A$(10)="MARS ROVER 2   MARS SPIRIT"
5100 LET A$(11)="ARTEMIS I MOON SNOOPY II"
5110 LET A$(12)="JUNO JUPITER   VULCAN"
5115 REM LOAD GRAVITY AND FUEL VALUES
5120 FOR I=1 TO 6
5130 LET G(I)=0.18
5140 LET F(I)=100/(I/2)
5150 NEXT I
5160 FOR I=7 TO 10
5170 LET G(I)=0.38
5180 LET F(I)=200/(I/2)
5190 NEXT I
5200 LET G(9)=0.91
5205 LET F(9)=300
5210 LET G(11)=0.18
5220 LET F(11)=100
5230 LET G(12)=2.4
5240 LET F(12)=500
5250 REM LOAD RANKS
5260 LET R$="ENSIGN      LIEUTENANT  LT COMMANDERCOMMANDER   CAPTAIN     COMMODORE   REAR ADMIRALVICE ADMIRALADMIRAL     FLT ADMIRAL "
5270 RETURN
6000 REM TITLE
6010 PRINT AT 0,0;CHR$(177);CHR$(186);CHR$(179);CHR$(166);CHR$(183);
6020 PRINT CHR$(128);CHR$(168);CHR$(180);CHR$(183);CHR$(181);CHR$(184);
6030 PRINT " U/DROID";CHR$(9);"MIKE 7/29/19"
6040 RETURN
6500 REM PAUSE ROUTINE--BUILT IN PAUSE FLICKERS PA IS IN SECONDS
6510 FOR I=1 TO PA
6520 POKE 16436,255
6530 IF PEEK 16436 > ZX AND INKEY$="" THEN GOTO 6530
6535 IF INKEY$ <> "" THEN RETURN
6540 NEXT I
6550 RETURN
7000 REM LANDING CODE
7010 GOSUB 1500
7020 IF ABS(XV)>1.5 OR ABS(YV)>1.5 THEN GOTO 9500
7030 GOSUB 7500
7035 PRINT
7040 LET MI=MI+1
7050 LET SC=SC+FU+(20*MI)
7051 REM BONUS FOR SOFT LANDING
7052 LET TM = 0
7053 IF ABS(XV)> 1 AND ABS(YV) > 1 THEN GOTO 7060
7054 IF ABS(XV) < 1 THEN LET TM=TM+(1/ABS(XV))
7055 IF ABS(YV) < 1 THEN LET TM=TM+(1/ABS(YV))
7056 IF TM > 100 THEN LET TM=100
7057 LET SC=SC+TM
7058 PRINT "***SOFT LANDING BONUS***"
7060 LET SC = INT(SC)
7065 PRINT "CAREER MISSION SCORE: ";SC
7070 IF (SC/100)+1 < RA THEN GOTO 7100
7080 LET RA=INT(SC/100)
7085 IF RA > 9 THEN LET RA = 9
7090 PRINT "YOU HAVE BEEN PROMOTED TO ",R$((RA*12)+1 to (RA*12)+12)
7100 PRINT "PRESS A KEY TO CONTINUE"
7110 LET PA=20
7112 GOSUB 6500 
7115 IF MI > 12 THEN GOTO 9000
7118 LET B$=""
7120 GOTO 3000 
7500 REM LANDNG SUBROUTINE
7533 PRINT AT Y+2,X-1;CHR$(10) + CHR$(10) + CHR$(10) + CHR$(10) + CHR$(10) 
7534 PRINT AT Y+1,X-1;" " + CHR$(6)+ " " + CHR$(134) + " "
7535 LET S$ = "THE "+A$(MI)(16 TO 30)+" HAS LANDED"
7540 REM LET XX = (COLS - LEN(S$)) / 2retry 
7545 LET XX = 1
7550 LET YY = (ROWS / 2)-1
7560 PRINT AT YY,XX;S$
7570 RETURN
8000 REM TUTORIAL
8010 LET B$="M"
8020 GOSUB 1500
8030 PRINT AT 6,0;"TUTORIAL: ";CHR$(178);" KEY TO THRUST RIGHT"
8040 LET PA=3
8050 GOSUB 6500 
8055 LET B$="N"
8060 GOSUB 1500
8070 PRINT AT 6,0;"AND USE THE ";CHR$(179);" KEY TO THRUST LEFT"
8082 GOSUB 6500 
8085 LET B$=" "
8086 GOSUB 1500
8092 PRINT AT 6,0;J$
8095 PRINT AT 6,0;"KEY ";CHR$(168);" TOGGLES GUIDANCE COMPUTER "
8100 GOSUB 8500
8120 PRINT AT 6,0;" PRESS ";CHR$(192);CHR$(168);CHR$(192);" AGAIN TO TURN IT OFF "
8127 GOSUB 6500 
8133 PRINT AT 0,0;J$
8134 PRINT J$
8135 GOSUB 1500
8136 PRINT AT 6,0;"    MAIN THRUST IS ALWAYS ON    "
8138 GOSUB 6500 
8140 PRINT AT 6,0;CHR$(192);CHR$(166);;CHR$(192);" SETS THRUST LEVEL. ENTER NOW"
8150 RETURN
8500 REM GUIDANCE COMPUTER
8510 FAST
8520 PRINT AT 0,0;J$
8530 PRINT J$
8540 PRINT AT 0,0;CHR$(173);CHR$(184);CHR$(181);CHR$(170);CHR$(170);CHR$(169);XV;" ";CHR$(172);G(MI);" ";
8545 PRINT CHR$(185);CHR$(173);CHR$(183);CHR$(186);CHR$(184);CHR$(185);TH
8550 PRINT CHR$(187);CHR$(184);CHR$(181);CHR$(170);CHR$(170);CHR$(169);YV;" ";CHR$(171);CHR$(186);CHR$(170);CHR$(177);FU
8560 SLOW
8570 LET PA=3
8575 GOSUB 6500 
8580 PRINT AT 0,0;J$
8590 PRINT J$ 
8600 RETURN
9000 REM END OF GAME
9002 FAST
9005 CLS
9007 SLOW
9010 PRINT "RETIREMENT HAS BEEN APPROVED"
9050 PRINT
9052 PRINT "YOUR MONTHLY PENSION IS: $";INT(SC/1.5);".00"
9054 PRINT
9055 PRINT "ENJOY YOUR RETIREMENT"
9057 PRINT
9058 PRINT "YOUR FINAL RANK IS:"
9059 PRINT R$((RA*12)+1 to (RA*12)+12)
9060 PRINT
9065 PRINT "CAREER MISSION SCORE IS: ";SC
9070 PRINT 
9115 PRINT "THE LUNAR CORPS WILL MISS YOU"
9120 PRINT
9130 PRINT "PRESS A KEY TO CONTINUE"
9140 LET PA=20
9145 GOSUB 6500 
9150 CLS
9151 LET MI=1
9155 GOTO 300
9160 END
9500 REM CRASH
9502 IF Y > 20 THEN LET Y=20
9505 GOSUB 1500
9510 PRINT AT Y+1,X;CHR$(136);CHR$(136);CHR$(136)
9520 FOR I=0 TO 6
9530 IF X-I > 0 THEN PRINT AT Y-I,X-I;CHR$(136)
9540 PRINT AT Y-I,X+1;CHR$(136)
9550 IF X+I < 29 THEN PRINT AT Y-I,X+I+2;CHR$(136)
9560 NEXT I
9565 PRINT AT 0,0;J$
9570 PRINT AT 0,0; A$(MI)(16 TO 30);" HAS CRASHED"
9580 PRINT "THERE WERE NO SURVIVORS"
9590 PRINT
9600 PRINT "CONDOLENCES TO THE FAMILY OF"
9605 PRINT R$((RA*12)+1 to (RA*12)+12);" ";n$
9610 PRINT
9620 PRINT "RETRY LAST MISSION? (Y OR N)"
9630 INPUT B$
9635 IF B$(1)="N" THEN GOTO 9057
9640 LET SC=SC/2
9650 LET RA=INT(RA/2)
9655 LET B$="R"
9660 GOTO 3300
